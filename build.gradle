plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'checkstyle'
    id 'com.google.protobuf' version '0.8.10'
    id 'net.researchgate.release' version '2.8.1'
    id 'org.sonarqube' version '2.8'
}

group = 'com.carepay'

configurations {
    testImplementation.extendsFrom compileOnly
}

repositories {
    mavenCentral()
    maven {
        url 's3://cp-maven-repo/releases'
        authentication {
            awsIm(AwsImAuthentication)
        }
    }
}

dependencies {
    implementation 'com.carepay:carepool-aws:1.0.0'

    compileOnly 'com.zaxxer:HikariCP:3.2.0'
    compileOnly 'org.apache.tomcat:tomcat-jdbc:9.0.21'

    testImplementation 'com.h2database:h2:1.4.199'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.current()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-parameters'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url 's3://cp-maven-repo/releases'
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}

checkstyle {
    config = project.resources.text.fromUri('https://bitbucket.org/carepaydev/carepay-config/raw/HEAD/checkstyle-1.0.xml')
}

release {
    tagCommitMessage = 'Release tag [skip ci]'
    newVersionCommitMessage = 'New version [skip ci]'
    git {
        requireBranch = ''
    }
}

afterReleaseBuild.dependsOn publish
